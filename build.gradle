plugins {
    id "java"
    id "org.springframework.boot" version "2.2.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "com.adarshr.test-logger" version "2.0.0"
    id "jacoco"
}

group = "com.fleboulch"
description = "treasure-map"
version = "1.0-SNAPSHOT"

sourceCompatibility = 14
targetCompatibility = 14

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    exclude "**/*IntTest*"
    jvmArgs '-Djunit.jupiter.displayname.generator.default=org.junit.jupiter.api.DisplayNameGenerator$ReplaceUnderscores'
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    description = "Execute integration tests."
    group = "verification"
    include "**/*IntTest*"

}

testlogger {
    theme = "mocha"
    slowThreshold = 1000
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false

        xml.destination file("$buildDir/reports/jacoco/report.xml")
    }
}

check.dependsOn integrationTest, jacocoTestReport

dependencies {

    // csv writer
    compile "com.opencsv:opencsv:5.0"

    // SB dependencies
    implementation "org.springframework.boot:spring-boot-starter-web"

    // test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junit_version}"
    testImplementation "org.assertj:assertj-core:3.14.0"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

}
